provider "aws" {
  region = var.region
}

data "aws_availability_zones" "available" {}

# Create VPC
resource "aws_vpc" "this" {
  cidr_block           = "10.0.0.0/16"
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "${var.stage}-vpc"
  }
}

# Create Public Subnets
resource "aws_subnet" "public" {
  count                   = 2
  vpc_id                  = aws_vpc.this.id
  cidr_block              = element(["10.0.0.0/24", "10.0.1.0/24"], count.index)
  availability_zone       = element(data.aws_availability_zones.available.names, count.index)
  map_public_ip_on_launch = true

  tags = {
    Name = "${var.stage}-public-subnet-${count.index + 1}"
  }
}

# Create Private Subnets
resource "aws_subnet" "private" {
  count             = 2
  vpc_id            = aws_vpc.this.id
  cidr_block        = element(["10.0.2.0/24", "10.0.3.0/24"], count.index)
  availability_zone = element(data.aws_availability_zones.available.names, count.index)

  tags = {
    Name = "${var.stage}-private-subnet-${count.index + 1}"
  }
}

# Create Internet Gateway
resource "aws_internet_gateway" "this" {
  vpc_id = aws_vpc.this.id

  tags = {
    Name = "${var.stage}-igw"
  }
}

# Create Public Route Table
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.this.id

  tags = {
    Name = "${var.stage}-public-rt"
  }
}

# Create Public Route
resource "aws_route" "public_internet_access" {
  route_table_id         = aws_route_table.public.id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = aws_internet_gateway.this.id
}

# Associate Route Table with Public Subnets
resource "aws_route_table_association" "public" {
  count          = 2
  subnet_id      = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public.id
}

# Create EIP for NAT Gateway
resource "aws_eip" "nat" {
  count  = 2
  domain = "vpc"

  tags = {
    Name = "${var.stage}-nat-eip-${count.index + 1}"
  }
}

# Create NAT Gateway
resource "aws_nat_gateway" "this" {
  count         = 2
  allocation_id = aws_eip.nat[count.index].id
  subnet_id     = aws_subnet.public[count.index].id

  tags = {
    Name = "${var.stage}-nat-gw-${count.index + 1}"
  }
}

# Create Private Route Table
resource "aws_route_table" "private" {
  count  = 2
  vpc_id = aws_vpc.this.id

  tags = {
    Name = "${var.stage}-private-rt-${count.index + 1}"
  }
}

# Create Private Route
resource "aws_route" "private_internet_access" {
  count                  = 2
  route_table_id         = aws_route_table.private[count.index].id
  destination_cidr_block = "0.0.0.0/0"
  nat_gateway_id         = aws_nat_gateway.this[count.index].id
}

# Associate Route Table with Private Subnets
resource "aws_route_table_association" "private" {
  count          = 2
  subnet_id      = aws_subnet.private[count.index].id
  route_table_id = aws_route_table.private[count.index].id
}

# Create Security Group for ALB
resource "aws_security_group" "alb_sg" {
  name        = "${var.stage}-public-lb-sg"
  description = "Access to public load balancer"
  vpc_id      = aws_vpc.this.id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Create Security Group for Container
resource "aws_security_group" "container_sg" {
  name        = "${var.stage}-container-sg"
  description = "Access to container service"
  vpc_id      = aws_vpc.this.id

  ingress {
    from_port       = 3000
    to_port         = 3000
    protocol        = "tcp"
    security_groups = [aws_security_group.alb_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Create Public Load Balancer
resource "aws_lb" "public" {
  name               = "${var.stage}-public-lb"
  load_balancer_type = "application"
  subnets            = aws_subnet.public[*].id
  security_groups    = [aws_security_group.alb_sg.id]

  tags = {
    Name = "${var.stage}-public-lb"
  }
}

# Create Dummy Target Group (optional)
resource "aws_lb_target_group" "dummy" {
  name     = "${var.stage}-dummy-tg"
  port     = 80
  protocol = "HTTP"
  vpc_id   = aws_vpc.this.id

  health_check {
    path                = "/"
    protocol            = "HTTP"
    interval            = 6
    timeout             = 5
    healthy_threshold   = 2
    unhealthy_threshold = 2
  }
}

# Create Listener for Public Load Balancer
resource "aws_lb_listener" "public_listener" {
  load_balancer_arn = aws_lb.public.arn
  port              = "80"
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.dummy.arn
  }
}

# Create a Security Group for RDS PostgreSQL
resource "aws_security_group" "rds_sg" {
  name        = "${var.stage}-rds-sg"
  description = "Security group for RDS PostgreSQL"
  vpc_id      = aws_vpc.this.id

  ingress {
    from_port       = 5432
    to_port         = 5432
    protocol        = "tcp"
    security_groups = [aws_security_group.container_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Create PostgreSQL RDS instance
resource "aws_db_instance" "postgres" {
  allocated_storage    = 20
  storage_type         = "gp2"
  engine               = "postgres"
  engine_version       = "16.4"
  instance_class       = "db.t3.micro"
  db_name              = var.database_name    # Use 'db_name' for the database name
  username             = var.database_user    # Corresponds to DATABASE_USER
  password             = var.database_password # Corresponds to DATABASE_PASSWORD
  parameter_group_name = "default.postgres16"
  skip_final_snapshot  = true
  publicly_accessible  = false
  vpc_security_group_ids = [aws_security_group.rds_sg.id]

  # Ensure you are deploying in the private subnets for better security.
  db_subnet_group_name = aws_db_subnet_group.rds.name
  multi_az             = false
  backup_retention_period = 7

  tags = {
    Name = "${var.stage}-rds-postgres"
  }
}

# Subnet group for RDS
resource "aws_db_subnet_group" "rds" {
  name       = "${var.stage}-rds-subnet-group"
  subnet_ids = aws_subnet.private[*].id

  tags = {
    Name = "${var.stage}-rds-subnet-group"
  }
}

# Output the RDS endpoint and database name
output "rds_endpoint" {
  description = "PostgreSQL RDS endpoint"
  value       = aws_db_instance.postgres.endpoint
}

output "rds_db_name" {
  description = "PostgreSQL database name"
  value       = aws_db_instance.postgres.db_name
}

# Create ECS Cluster
resource "aws_ecs_cluster" "this" {
  name = "${var.stage}-ecs-cluster"
}

# Create Security Group for EFS
resource "aws_security_group" "efs_sg" {
  name        = "${var.stage}-efs-sg"
  description = "Security group for EFS file system"
  vpc_id      = aws_vpc.this.id

  ingress {
    from_port       = 2049
    to_port         = 2049
    protocol        = "tcp"
    security_groups = [aws_security_group.container_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Create EFS File System
resource "aws_efs_file_system" "this" {
  encrypted        = true
  performance_mode = "generalPurpose"
  throughput_mode  = "bursting"

  tags = {
    Name = "${var.stage}-efs"
  }
}

# Create EFS Mount Targets
resource "aws_efs_mount_target" "efs_mt" {
  count           = 2
  file_system_id  = aws_efs_file_system.this.id
  subnet_id       = aws_subnet.private[count.index].id
  security_groups = [aws_security_group.efs_sg.id]
}

# Create IAM Role for ECS Service
resource "aws_iam_role" "ecs_service_role" {
  name = "${var.stage}-ecs-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Effect = "Allow",
      Principal = {
        Service = "ecs.amazonaws.com"
      },
      Action = "sts:AssumeRole"
    }]
  })

  managed_policy_arns = ["arn:aws:iam::aws:policy/CloudWatchAgentServerPolicy"]
}

# Attach Policy to ECS Service Role
resource "aws_iam_role_policy" "ecs_service_policy" {
  name = "ecs-service-policy"
  role = aws_iam_role.ecs_service_role.id

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Effect = "Allow",
      Action = [
        "ec2:AttachNetworkInterface",
        "ec2:CreateNetworkInterface",
        "ec2:CreateNetworkInterfacePermission",
        "ec2:DeleteNetworkInterface",
        "ec2:DeleteNetworkInterfacePermission",
        "ec2:Describe*",
        "ec2:DetachNetworkInterface",
        "elasticloadbalancing:DeregisterInstancesFromLoadBalancer",
        "elasticloadbalancing:DeregisterTargets",
        "elasticloadbalancing:Describe*",
        "elasticloadbalancing:RegisterInstancesWithLoadBalancer",
        "elasticloadbalancing:RegisterTargets"
      ],
      Resource = "*"
    }]
  })
}

# Create IAM Role for ECS Task Execution
resource "aws_iam_role" "ecs_task_execution_role" {
  name = "${var.stage}-ecs-task-execution-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Effect = "Allow",
      Principal = {
        Service = "ecs-tasks.amazonaws.com"
      },
      Action = "sts:AssumeRole"
    }]
  })
}

# Attach Policy to ECS Task Execution Role
resource "aws_iam_role_policy" "ecs_task_execution_policy" {
  name = "ecs-task-execution-policy"
  role = aws_iam_role.ecs_task_execution_role.id

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Action = [
          "ecr:GetAuthorizationToken",
          "ecr:BatchCheckLayerAvailability",
          "ecr:GetDownloadUrlForLayer",
          "ecr:BatchGetImage",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ],
        Resource = "*"
      },
      {
        Effect = "Allow",
        Action = [
          "elasticfilesystem:ClientMount",
          "elasticfilesystem:ClientWrite",
          "elasticfilesystem:DescribeMountTargets",
          "elasticfilesystem:DescribeFileSystems"
        ],
        Resource = aws_efs_file_system.this.arn
      }
    ]
  })
}

# Create CloudWatch Log Group
resource "aws_cloudwatch_log_group" "flowise" {
  name              = "/ecs/${var.stage}"
  retention_in_days = 7
}

# Create ECS Task Definition
resource "aws_ecs_task_definition" "flowise" {
  family                   = "${var.stage}-flowise-task"
  execution_role_arn       = aws_iam_role.ecs_task_execution_role.arn
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  cpu                      = "256"
  memory                   = "512"

  container_definitions = jsonencode([
    {
      name      = "flowise-service"
      image     = "flowiseai/flowise:latest"
      essential = true
      portMappings = [
        {
          containerPort = 3000
          protocol      = "tcp"
        }
      ]
      environment = [
        { name = "PORT", value = "3000" },
        ### Add the following environment variables if using PostgreSQL/MySQL/MariaDB
        # { name = "DATABASE_TYPE", value = "postgres" },
        # { name = "DATABASE_HOST", value = aws_db_instance.postgres.endpoint },
        # { name = "DATABASE_PORT", value = "5432" }, 
        # { name = "DATABASE_NAME", value = var.database_name },
        # { name = "DATABASE_USER", value = var.database_user },
        # { name = "DATABASE_PASSWORD", value = var.database_password },
        { name = "CORS_ORIGINS", value = "*" },
        { name = "IFRAME_ORIGINS", value = "*" },
        { name = "DATABASE_PATH", value = "/root/.flowise" },
        { name = "LOG_PATH", value = "/root/.flowise/logs" },
        { name = "BLOB_STORAGE_PATH", value = "/root/.flowise/storage" },
        { name = "APIKEY_STORAGE_TYPE", value = "db" },
        { name = "DEBUG", value = "true" },
        # A secretkey that is used for securely encrypt credentials. You can generate a strong key using `openssl rand -base64 42`
        { name = "FLOWISE_SECRETKEY_OVERWRITE", value = "sk-9f73s3ljTXVcMT3Blb3ljTqtsKiGHXVcMT3BlbkFJLK7U" },
        { name = "FLOWISE_USERNAME", value = "user" },
        { name = "FLOWISE_PASSWORD", value = "1234" },
        # Add required environment variables here.
      ]
      entryPoint = ["flowise", "start"]
      mountPoints = [
        {
          sourceVolume  = "efs-volume"
          containerPath = "/root/.flowise"
        }
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options   = {
          "awslogs-group"         = aws_cloudwatch_log_group.flowise.name
          "awslogs-region"        = var.region
          "awslogs-stream-prefix" = var.stage
        }
      }
    }
  ])

  volume {
    name = "efs-volume"

    efs_volume_configuration {
      file_system_id     = aws_efs_file_system.this.id
      root_directory     = "/"
      transit_encryption = "ENABLED"
    }
  }
}

# Create Target Group for Flowise
resource "aws_lb_target_group" "flowise" {
  name        = "${var.stage}-flowise-tg"
  port        = 3000
  protocol    = "HTTP"
  target_type = "ip"
  vpc_id      = aws_vpc.this.id

  health_check {
    path                = "/"
    protocol            = "HTTP"
    interval            = 10
    timeout             = 5
    healthy_threshold   = 2
    unhealthy_threshold = 3
    matcher             = "200-299"
  }
}

# Create Listener Rule for Flowise
resource "aws_lb_listener_rule" "flowise" {
  listener_arn = aws_lb_listener.public_listener.arn
  priority     = 1

  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.flowise.arn
  }

  condition {
    path_pattern {
      values = ["/*"]
    }
  }
}

# Create ECS Service
resource "aws_ecs_service" "flowise" {
  name            = "${var.stage}-flowise-service"
  cluster         = aws_ecs_cluster.this.id
  task_definition = aws_ecs_task_definition.flowise.arn
  launch_type     = "FARGATE"
  desired_count   = 2

  network_configuration {
    subnets         = aws_subnet.private[*].id
    security_groups = [aws_security_group.container_sg.id]
    assign_public_ip = false
  }

  load_balancer {
    target_group_arn = aws_lb_target_group.flowise.arn
    container_name   = "flowise-service"
    container_port   = 3000
  }

  depends_on = [
    aws_lb_listener_rule.flowise,
    aws_route.private_internet_access
  ]
}

# Output the external URL
output "external_url" {
  description = "URL of the Flowise application"
  value       = "http://${aws_lb.public.dns_name}"
}
